subprojects {
    group = 'clojuresque'
    version = '1.5.8-SNAPSHOT'

    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

    apply {
        plugin 'signing'
        plugin 'maven'
    }

    repositories {
        maven { url "http://127.0.0.1:8080/repository/internal" }
        mavenCentral()
    }
}

project(':clojuresque') {
    apply {
        plugin 'groovy'
    }

    dependencies {
        compile gradleApi()
        // Workaround for milestone4
        compile fileTree(dir: "${gradle.gradleHomeDir}/lib/plugins", include: '**/*.jar')
        groovy localGroovy()
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            expand("version": project.version)
        }
    }
}

project(':runtime') {
    apply {
        plugin 'java'
    }

    configurations {
        clojure
    }

    dependencies {
        clojure 'org.clojure:clojure:[1.2,1.6)'
    }

    sourceSets.main {
        compileClasspath = project.files(
            compileClasspath,
            project.configurations.clojure
        )
    }
}

subprojects {
    configurations {
        deployerJars
    }

    dependencies {
        deployerJars 'org.apache.maven.wagon:wagon-http-lightweight:2.2'
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                configuration = configurations.deployerJars
                repository(url: "https://clojars.org/repo") {
                    authentication(
                        userName: project.properties["clojuresque.clojars.username"],
                        password: project.properties["clojuresque.clojars.password"]
                    )
                }
                pom.project {
                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                    description "A Clojure plugin for the Gradle build system"
                    url "http://bitbucket.org/clojuresque/clojuresque"
                    scm {
                        connection 'scm:hg:hg@bitbucket.org:clojuresque/clojuresque'
                        developerConnection 'scm:hg:hg@bitbucket.org:clojuresque/clojuresque'
                        url 'hg@bitbucket.org:clojuresque/clojuresque'
                    }
                }
                beforeDeployment { signing.signPom(it) }
            }
        }
    }

    signing {
        sign configurations.archives
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.0"
}
