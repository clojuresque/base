plugins {
    id "com.jfrog.bintray" version "1.4"
    id "com.gradle.plugin-publish" version "0.9.1"
    id "de.kotka.project" version "1.5.1"
}

apply plugin: 'java'
apply plugin: 'groovy'

group = 'de.kotka.clojuresque'
version = '1.8.0-SNAPSHOT'
description = "A Clojure plugin for gradle (base)"

kotka {
    wrapperVersion = "2.7"
    licenses   = [ "EUPL-1.1", "MIT" ]
    repository = "clojuresque/base"
    bintray    = [ repo: "clojuresque" ]
}

repositories {
    jcenter()
}

ext.clojuresquePlugins = [
    common: "2.0.0"
]

dependencies {
    compile gradleApi()
    compile localGroovy()

    project.clojuresquePlugins.each { plugin, version ->
        compile "de.kotka.clojuresque:clojuresque-${plugin}:${version}"
    }
    compile "de.kotka.gradle:gradle-utils:0.2.2"
    compile "org.clojure:clojure:1.7.0"
    compile "org.clojure:tools.namespace:0.2.11"

    testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

compileJava {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

compileGroovy {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

pluginBundle {
    website = 'https://bitbucket.org/clojuresque/base'
    vcsUrl = 'https://bitbucket.org/clojuresque/base'
    description = project.description
    tags = ['clojure']

    plugins {
        basePlugin {
            id = 'de.kotka.clojuresque.base'
            displayName = project.description
        }
    }
}
