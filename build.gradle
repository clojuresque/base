subprojects {
    group = 'clojuresque'
    version = '1.4.2-SNAPSHOT'
}

project(':clojuresque') {
    apply {
        plugin 'groovy'
        plugin 'maven'
    }

    dependencies {
        compile gradleApi()
        // Workaround for milestone4
        compile fileTree(dir: "${gradle.gradleHomeDir}/lib/plugins", include: '**/*.jar')
        groovy localGroovy()
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            expand("version": project.version)
        }
    }
}

project(':runtime') {
    apply {
        plugin 'java'
        plugin 'maven'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.clojure:clojure:1.+'
    }
}

subprojects {
    uploadArchives.doLast {
        String pomName = project.buildDirName + "/" + project.pomDirName + "/" +
            project.name + "-" + project.version + ".xml"

        project.pom().writeTo(pomName)

        project.exec {
            commandLine(
                '/usr/bin/scp',
                project.file(project.tasks.jar.archivePath).path,
                project.file(pomName).path,
                'clojars@clojars.org:'
            )
        }
    }
}

