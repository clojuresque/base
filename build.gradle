subprojects {
    group = 'clojuresque'
    version = '1.5.3-SNAPSHOT'

    apply plugin: 'signing'
}

project(':clojuresque') {
    apply {
        plugin 'groovy'
        plugin 'maven'
    }

    dependencies {
        compile gradleApi()
        // Workaround for milestone4
        compile fileTree(dir: "${gradle.gradleHomeDir}/lib/plugins", include: '**/*.jar')
        groovy localGroovy()
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            expand("version": project.version)
        }
    }
}

project(':runtime') {
    apply {
        plugin 'java'
        plugin 'maven'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.clojure:clojure:[1.2,1.5)'
    }
}

subprojects {
    ext.pomFile = project.buildDir.path + "/" +
            project.mavenPomDir.name + "/" +
            project.name + "-" + project.version + ".xml"

    task writePom {
        outputs.file project.pomFile
    } << {
        project.pom().writeTo(project.pomFile)
    }

    artifacts {
        writePom.outputs.files.each { archives it }
    }

    signing {
        sign configurations.archives
    }

    signArchives {
        dependsOn writePom
    }

    uploadArchives {
        dependsOn project.writePom
    } << {
        project.exec {
            commandLine(
                '/usr/bin/scp',
                project.pomFile,
                project.pomFile + ".asc",
                project.jar.archivePath.path,
                project.jar.archivePath.path + ".asc",
                'clojars@clojars.org:'
            )
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.0"
}
