buildscript {
    repositories {
        mavenCentral()
        maven { url "http://clojars.org/repo" }
    }

    dependencies {
        classpath "de.kotka.gradle:gradle-plugin:1.2.0"
    }
}

subprojects {
    group = 'clojuresque'
    version = '1.6.0'

    ext.kotka = [
        repository:     "clojuresque/base",
        wrapperVersion: "1.10"
    ]

    apply plugin: 'java'
    apply plugin: 'kotka'

    repositories {
        mavenCentral()
    }

    ext.clojuresquePlugins = [
        common: "1.1.0"
    ]
}

project(':clojuresque-base') {
    description = "A Clojure plugin for gradle (base)"

    apply {
        plugin 'groovy'
    }

    dependencies {
        compile gradleApi()
        compile localGroovy()

        project.clojuresquePlugins.each { plugin, version ->
            compile "clojuresque:clojuresque-${plugin}:${version}"
        }
        compile "de.kotka.gradle:gradle-utils:0.2.1"

        testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        }
    }

    compileJava {
        sourceCompatibility = "1.6"
        targetCompatibility = "1.6"
    }

    compileGroovy {
        sourceCompatibility = "1.6"
        targetCompatibility = "1.6"
    }

    processResources {
        inputs.property "version", {-> project.version}
        from(sourceSets.main.resources.srcDirs) {
            expand("version": project.version)
        }
    }
}

project(':clojuresque-base-runtime') {
    description = "A Clojure plugin for gradle (base runtime files)"

    dependencies {
        project.clojuresquePlugins.each { plugin, version ->
            compile "clojuresque:clojuresque-${plugin}-runtime:${version}"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.10"
}
