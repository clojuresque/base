subprojects {
    group = 'clojuresque'
    version = '1.6.0-SNAPSHOT'
}

project(':clojuresque') {
    apply plugin: 'groovy'

    dependencies {
        compile gradleApi()
        // Workaround for milestone4
        compile fileTree(dir: "${gradle.gradleHomeDir}/lib/plugins", include: '**/*.jar')
        groovy localGroovy()
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            expand("version": project.version)
        }
    }
}

project(':clojuresque-runtime') {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.clojure:clojure:[1.2,1.4)'
    }
}

project(':scriptoresque') {
    apply plugin: 'groovy'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile gradleApi()
        // Workaround for milestone4
        compile fileTree(dir: "${gradle.gradleHomeDir}/lib/plugins", include: '**/*.jar')
        compile project(':clojuresque')

        groovy localGroovy()
    }
}

project(':scriptoresque-runtime') {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile project(':clojuresque-runtime')
    }
}

subprojects {
    apply plugin: 'maven'

    uploadArchives.doLast {
        String pomFile = project.buildDir.path + "/" +
            project.mavenPomDir.name + "/" +
            project.name + "-" + project.version + ".xml"

        project.pom().writeTo(pomFile)

        project.exec {
            commandLine(
                '/usr/bin/scp',
                pomFile,
                project.jar.archivePath.path,
                'clojars@clojars.org:'
            )
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.0"
}
