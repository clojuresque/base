buildscript {
    dependencies {
        classpath files("gradle/plugins/clojars-1.0.0.jar")
    }
}

import clojuresque.ClojarsPlugin

subprojects {
    group = 'clojuresque'
    version = '1.6.0-SNAPSHOT'

    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

    apply {
        plugin 'java'
        plugin 'signing'
        plugin ClojarsPlugin
    }

    repositories {
        maven { url "http://127.0.0.1:8080/repository/internal" }
        mavenCentral()
    }

    clojars {
        deploy(uploadArchives) {
            project {
                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                description "A Clojure plugin for the Gradle build system"
                url "http://bitbucket.org/clojuresque/clojuresque"
                scm {
                    connection 'scm:hg:hg@bitbucket.org:clojuresque/clojuresque'
                    developerConnection 'scm:hg:hg@bitbucket.org:clojuresque/clojuresque'
                    url 'hg@bitbucket.org:clojuresque/clojuresque'
                }
            }
        }
    }

    signing {
        sign configurations.archives
    }
}

project(':clojuresque') {
    apply {
        plugin 'groovy'
    }

    dependencies {
        compile gradleApi()
        // Workaround for milestone4
        compile fileTree(dir: "${gradle.gradleHomeDir}/lib/plugins", include: '**/*.jar')
        compile localGroovy()

        testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        }
    }

    processResources {
        inputs.property "version", {-> project.version}
        from(sourceSets.main.resources.srcDirs) {
            expand("version": project.version)
        }
    }
}

project(':clojuresque-runtime') {
    configurations {
        clojure
    }

    dependencies {
        clojure 'org.clojure:clojure:1.5.1'
    }

    sourceSets.main {
        compileClasspath = project.files(
            compileClasspath,
            project.configurations.clojure
        )
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.6"
}
